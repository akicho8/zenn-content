---
title: "transient.el を使いこなしたい"
emoji: "🐡"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["Emacs", "Transient"]
published: true
---

## 一式入ってるいちばん簡単な例 ##

```emacs-lisp
(require 'transient)

(transient-define-prefix f1 ()
  [("h" "Hello" (lambda () (interactive) (message "こんにちは")))])

(global-set-key (kbd "C-c RET") 'f1)
```
```text
 h Hello
```

- `M-x f1` または `C-c RET` でメニューが起動する
- `h` で lambda ブロックを実行する

## description を入れる ##

`"コマンド"` の部分のこと。なくてもいいけどあるとわかりやすくなる

```emacs-lisp
(transient-define-prefix f1 ()
  ["コマンド" ("h" "Hello" (lambda () (interactive) (message "こんにちは")))])
```

```text
コマンド
 h Hello
```

## 指定の関数を実行する ##

```emacs-lisp
(defun c1() (interactive) (message "c1"))

(transient-define-prefix f1 ()
  [("a" "item1" c1)])
```

- lambda のかわりに関数を指定する
- `a item1` と表示され `a` で関数 `c1` を実行する

## 関数を複数指定する ##

```emacs-lisp
(defun c1() (interactive) (message "c1"))
(defun c2() (interactive) (message "c2"))

(transient-define-prefix f1 ()
  [
   ("a" "item1" c1)
   ("b" "item2" c2)
   ])
```

要素を複数書くと項目が**縦**にならぶ

```text
a item1
b item2
```

## 実行してもメニューを閉じないようにする ##

```emacs-lisp
(transient-define-prefix f1 ()
  [
   ("a" "item1" c1 :transient t)
   ])
```

- `:transient t` をつけると `a` を押してもメニューが閉じなくなる
- 何回か連続で実行するような関数に指定する
- `C-q` `C-g` `ESC ESC ESC` のどれかでメニューを閉じれる

## 関数の方にパラメータを書いてもいい ##

`defun` のかわりに `transient-define-suffix` を使うと関数側でパラメータを書ける

```emacs-lisp
(transient-define-suffix c1 ()
  :key "a"
  :description "item1"
  :transient t
  (interactive)
  (message "c1"))
```

これでメニューの方には関数名を並べるだけでよくなる

```emacs-lisp
(transient-define-prefix f1 ()
  [(c1)])
```

設定はメニュー側で上書きできる

```emacs-lisp
(transient-define-prefix f1 ()
  [(c1 :key "b")])
```

普通の関数だったかのように書いてもいい

```emacs-lisp
(transient-define-prefix f1 ()
  [("b" "item2" c1)])
```

## レイアウト ##

これを頭で理解するのは難しい

#### 「配列」を並べると下方向に増える ####

```emacs-lisp
(transient-define-prefix f1 ()
  ["上" (a) (b)]
  ["下" (c) (d)])
```
```text
上
 a
 b
下
 c
 d
```

#### 「配列の配列」なら右方向に増える ####

```emacs-lisp
(transient-define-prefix f1 ()
  [["左" (a) (b)]["右" (c) (d)]])
```
```text
左    右
 a     c
 b     d
```

#### 「配列の配列」を並べるとそれが下方向に増える ####

```emacs-lisp
(transient-define-prefix f1 ()
  ["上" ["左上" (a) (b)] ["右上" (c) (d)]]
  ["下" ["左下" (a) (b)] ["右下" (c) (d)]])
```
```text
左上    右上
  a       c
  b       d
左下    右下
  a       c
  b       d
```

## description は動的に生成できる ##

  * 次の2つは同じ
  * 動的に生成できるが役立ったことはいまのところない

```emacs-lisp
(transient-define-prefix f1 ()
  ["上" (c1)])
```

```emacs-lisp
(transient-define-prefix f1 ()
  [:description (lambda () "上") (c1)])
```

## 引数・オプション ##

#### 基本形 ####

```emacs-lisp
(transient-define-prefix f1 ()
  ["オプション" ("-x" "論理型" "--xxx")]
  ["コマンド" ("a" "item1" (lambda () (interactive) (prin1 (transient-args 'f1))))])
```

```text
オプション
 -x 論理型 (--xxx)

コマンド
 a item1
```

  * すぐに `a` をタイプすると `nil` を表示する
  * `-x` をタイプすると `--xxx` が有効になる (わかりにくいが色が少し変わる)
  * `--xxx` を有効にしてから `a` をタイプすると `("--xxx")` を表示する
  * `(transient-args 'f1)` がオプションの**配列**を返している
    * 引数の `f1` の部分は対象の関数名
    * オプションがないときは空配列ではなく `nil` になる
      * が、それが問題になることはとくにない
  * `-x` の部分はハイフンで始めなくてもいい
    * `x` とすれば `x` でトグルできる
    * しかしコマンドと操作が干渉する心配があるので `-` で始めた方がいい
  * `"オプション"` と `"コマンド"` は無くてもいいがあるとわかりやすい
  * オプションとコマンドをどこに書くか決まりはない
    * 見てわかるように配列内要素の3つ目が関数かどうかで見分けているようだ
    * だからオプション類は下に配置してもいい

#### 初期値を指定する ####

```emacs-lisp
(transient-define-prefix f1 ()
  :value '("--xxx")
  ["オプション" ("-x" "論理型" "--xxx")]
  ["コマンド" ("a" "item1" (lambda () (interactive) (prin1 (transient-args 'f1))))])
```

  * `:value` で初期値を指定する
  * 上の例ではメニューを起動した時点で `--xxx` に有効になっている (色がついている)

#### オプションを永続化する ####

`C-x` を押すとこうなるので

![](/images/06cb8d002603db/emacs_ss1.png)
*C-x を押したところ*

続けて `C-s` するとファイルに保存される

```emacs-lisp:~/.emacs.d/transient/values.el
((f1 "--xxx"))
```

よく使うかもしれない操作まとめ

| 操作      | 意味           | 備考                    |
|-----------|----------------|-------------------------|
| `C-x C-s` | ファイルに保存 | 永続化                  |
| `C-x s`   | メモリに保存   | Emacsを閉じたら元に戻る |
| `C-x C-k` | 初期値に戻す   |                         |

#### 文字列型のオプション ####

`--xxx` を `--xxx=` に変更する

```emacs-lisp
(transient-define-prefix f1 ()
  ["オプション" ("-x" "文字列型" "--xxx=")]
  ["コマンド" ("a" "item1" (lambda () (interactive) (prin1 (transient-args 'f1))))])
```

  * `-x` をタイプするとプロンプトが出る
  * `foo` と入力すると `--xxx=foo` と表示が変わる
  * その後で `a` をタイプすると `("--xxx=foo")` を表示する
  * 論理型と同様に初期値を書ける。例: `:value '("--xxx=foo")`

#### オプションの値を取り出す ####

汎用の `transient-arg-value` で配列から特定の値だけをええ感じに取り出せる

それぞれ論理型と文字列型の例

```emacs-lisp
(transient-arg-value "--xxx" '("--xxx"))      ; => t
(transient-arg-value "--xxx=" '("--xxx=foo")) ; => "foo"
```

となるので第二引数にはオプションの配列 `(transient-args 'f1)` を渡せばよい

```emacs-lisp
(transient-args 'f1)                                ; => ("--xxx=foo")
(transient-arg-value "--xxx=" (transient-args 'f1)) ; => "foo"
```

#### 文字列型オプションの `=` の有無はかなり重要 ####

  * `--xxx=foo` ではなく `--xxx foo` としてしまうと値をを取り出せない
  * 素直に `--xxx=foo` となるようにしよう

```emacs-lisp
(transient-arg-value "--xxx " '("--xxx foo")) ; => nil
```

#### オプションから外部コマンドの組み立て ####

オプションの捌き方はさまざまだけど外部コマンドの仕様と一致している場合はスムーズに渡せる

そのまま `git` の引数とする例:

```emacs-lisp
(require 's)
(shell-command (s-join " " (cons "git" (transient-args 'f1))))
```

`(transient-args 'f1)` が `("--version")` だとすれば `git --version` を実行する

## 項目の表示をモードで分ける ##

```emacs-lisp
(transient-define-prefix f1 ()
  [
   ("a" "item1" c1 :if-mode org-mode)
   ("b" "item2" c2 :if-mode emacs-lisp-mode)
   ("c" "item2" c3 :if-mode (org-mode emacs-lisp-mode))
   ])
```

- `:if-mode モード名` でそのモードのときだけ表示する
- モード名は配列で指定できる

## 実用編 ##

### ディレクトリ移動 ###

```emacs-lisp
(transient-define-prefix f1 ()
  "ディレクトリ移動"
  ["ディレクトリ移動"
   ("l" "ダウンロード" (lambda () (interactive) (dired "~/Downloads")))
   ("t" "デスクトップ" (lambda () (interactive) (dired "~/Desktop")))
   ("d" "書類"         (lambda () (interactive) (dired "~/Documents")))
   ("i" "画像"         (lambda () (interactive) (dired "~/Pictures")))
   ("x" "Dropbox"      (lambda () (interactive) (dired "~/Dropbox")))
   ("~" "Home"         (lambda () (interactive) (dired "~/")))
   ("s" "src"          (lambda () (interactive) (dired "~/src")))
   ("g" "gems"         (lambda () (interactive) (dired "/usr/local/var/rbenv/versions/3.1.0/lib/ruby/gems/3.1.0/gems")))
   ("z" "Zenn"         (lambda () (interactive) (dired "~/src/zenn-content/articles")))
   ("c" "Cargo"        (lambda () (interactive) (dired "~/.cargo/registry/src")))
   ("e" "Emacs"        (lambda () (interactive) (dired "~/.emacs.d")))
   ])
```

こうやって登録しておけばディレクトリ移動時の距離の感覚はなくなっていく

### Railsアプリ内でディレクトリ移動 ###

`.git` の親ディレクトリからの相対的な移動の例

```emacs-lisp
(transient-define-prefix f1 ()
  "Railsアプリ内の規定ディレクトリに移動する"
  ["ディレクトリ移動"
   ("l" "log"        (lambda () (interactive) (my-chdir-from-git-root "log")))
   ("m" "model"      (lambda () (interactive) (my-chdir-from-git-root "app/models")))
   ("c" "controller" (lambda () (interactive) (my-chdir-from-git-root "app/controllers")))
   ("t" "test"       (lambda () (interactive) (my-chdir-from-git-root "spec")))
   ])

(defun my-chdir-from-git-root (dir)
  "ディレクトリ移動(.gitの親から相対的に)"
  (interactive)
  (dired
   (concat
    (locate-dominating-file default-directory ".git")
    dir)))
```

##### 入力がバッティングしたらどうする？ #####

仮に最初につくった方と混ぜたとすると `l` が両方にあるので干渉してしまう
そういう場合は片方を2文字にする手もある

```emacs-lisp
(transient-define-prefix f1 ()
  ["ディレクトリ移動"
   ("l" "ダウンロード" (lambda () (interactive) (dired "~/Downloads")))
   ("rl" "log"         (lambda () (interactive) (my-chdir-from-git-root "log")))
   ])
```

ただ magit でこのような定義は見たことがない
徹底して1文字にこだわっているし、その方がいいと思う

## 参照 ##

https://github.com/magit/transient/wiki/Developer%20Quick%20Start%20Guide
https://magit.vc/manual/transient.html
https://github.com/magit/transient
