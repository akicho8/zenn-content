---
title: "Emacs から Zenn を使いやすくしてみた"
emoji: "🐬"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["Emacs", "Zenn", "transient", "helm"]
published: true
---

Zenn は zenn-cli があるおかげでとても記事が書きやすいです
ただ慣れてくるとちょっとした一手間が面倒になってきます
たとえば Typo を修正したいとき

  1. 記事のディレクトリに移動する
  2. 該当の Markdown ファイルを探す (もちろんファイル名からは予測つかない)
  3. プレビュー用のサービスを起動する (すでに起動してるのにまた起動しちゃったり)
  4. ブラウザからその記事をまた探す (だいたい1番上にあるけど)

となってまずファイルを探すので疲れてもう確認はいいからプッシュしちゃえとなりがちでした
なので Emacs からもっと簡単に使えるようにしてみました

どこからでもいいので `C-j z` をタイプすると次のようにメニューが出ます

![](/images/66b3769294b04b/emacs_ss1.png)
*メニューを起動したところ*

あとはやりたいことを適当に選択するだけです

### s 検索 ###

![](/images/66b3769294b04b/emacs_ss2.png)
*s のあと rust と入力したところ*

  * helm-ag を使って `title:` で絞り込んだ状態で起動する
  * そのまま選んでもいいし、絞り込んでもいい

### n 新規記事 ###

  * `zenn new:article` を実行して articles ディレクトリを dired で開く
  * 本当は Markdown ファイルを直接開きたかった
  * zenn コマンドの色付けを無効化する方法がわかんなかったので断念

### o 本家で表示 ###

  * 開いている Markdown ファイルに対応する本家のURLを開く

### p プレビュー ###

  * 開いている Markdown ファイルに対応するローカルのURLを直接開く
  * プレビュー用のサービスを起動させたかどうか気にとめる必要なし
  * 起動してなかったら勝手に起動する

### u パッケージ更新 ###

  * `npm install zenn-cli@latest` を実行する
  * 更新せいと言われたとき用

# コード #

```emacs-lisp
(require 'transient)
(require 'helm-ag)

(transient-define-suffix my-zenn-search ()
  :key "s"
  :description "検索"
  (interactive)
  (helm-do-ag "~/src/zenn-content" nil "title: "))

(transient-define-suffix my-zenn-article-new ()
  :key "n"
  :description "新規記事"
  (interactive)
  (shell-command "cd ~/src/zenn-content && zenn new:article")
  (dired "~/src/zenn-content/articles"))

(transient-define-suffix my-zenn-current-open ()
  :key "o"
  :description "本家で表示"
  (interactive)
  (let* ((stem (file-name-sans-extension (file-name-nondirectory buffer-file-name)))
        (url (format "https://zenn.dev/megeton/articles/%s" stem)))
    (shell-command (format "open %s" url))))

(transient-define-suffix my-zenn-current-preview ()
  :key "p"
  :description "プレビュー"
  (interactive)
  (let* ((stem (file-name-sans-extension (file-name-nondirectory buffer-file-name)))
         (url (format "http://localhost:8001/articles/%s" stem)))
    (my-zenn-preview-start)
    (shell-command (format "open %s" url))))

(transient-define-suffix my-zenn-preview-start ()
  :key "1"
  :description "プレビュー起動"
  (interactive)
  (when (not (process-status "zenn"))
    (start-process-shell-command "zenn" "*Zenn*" "cd ~/src/zenn-content && zenn preview --open -p 8001")))

(transient-define-suffix my-zenn-preview-stop ()
  :key "0"
  :description "プレビュー停止"
  (interactive)
  (when (process-status "zenn")
    (delete-process "zenn")))

(transient-define-suffix my-zenn-update ()
  :key "u"
  :description "パッケージ更新"
  (interactive)
  (async-shell-command "cd ~/src/zenn-content && npm install zenn-cli@latest"))

(transient-define-prefix my-zenn-menu ()
  "Zenn"
  [["Basic"
    (my-zenn-search)
    (my-zenn-article-new)
    (my-zenn-current-open)]
   ["Preview"
    (my-zenn-current-preview)
    (my-zenn-preview-start)
    (my-zenn-preview-stop)]
   ["Other"
    (my-zenn-update)]])

;; (global-set-key (kbd "C-j z") 'my-zenn-menu)
```
